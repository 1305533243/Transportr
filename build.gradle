buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.4.0-beta6'
	}
}

repositories {
	jcenter()
}

apply plugin: 'com.android.application'

dependencies {
	compile 'com.android.support:appcompat-v7:23.1.0'
	compile 'com.android.support:design:23.1.0'
	compile 'com.android.support:cardview-v7:23.1.0'
	compile 'com.android.support:recyclerview-v7:23.1.0'
	compile 'com.android.support:preference-v7:23.1.0'

	compile 'com.github.orangegangsters:swipy:1.2.0'
	compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
	compile('com.mikepenz:materialdrawer:4.3.7') {
		transitive = true
	}
	compile('com.mikepenz:aboutlibraries:5.2.5') {
		transitive = true
	}
	compile project(':sublibs:OSMBonusPack:OSMBonusPack')
	compile project(':sublibs:public-transport-enabler:enabler')
}

project(':sublibs:public-transport-enabler:enabler') {
	repositories {
		jcenter()
	}
}

project(':sublibs:OSMBonusPack:OSMBonusPack') {
	repositories {
		jcenter()
	}
}

android {
	defaultConfig {
		versionCode 17
		versionName "1.0.0-alpha1"

		compileSdkVersion 23
		targetSdkVersion 23
		buildToolsVersion "23.0.1"
		minSdkVersion 12
	}

	buildTypes {
		release {
			resValue "string", "app_name", "Transportr"
		}

		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			resValue "string", "app_name", "Transportr Devel"
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (project.hasProperty('signingStoreLocation') &&
			project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') &&
			project.hasProperty('signingKeyPassword')) {
		println "Found sign properties in gradle.properties! Signing buildâ€¦"

		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}

		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	// Do not abort build if lint finds errors
	lintOptions {
		abortOnError false
	}
}

