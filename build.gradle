buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.2.3'
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'com.android.application'

dependencies {
	compile 'com.android.support:appcompat-v7:22.1.1'
	compile 'com.android.support:cardview-v7:22.1.0'
	compile 'com.android.support:recyclerview-v7:22.1.0'
	compile "com.android.support:gridlayout-v7:22.1.0"
	compile 'com.github.orangegangsters:swipy:1.2.0'
	compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
	compile 'com.nispok:snackbar:2.10.7'
	compile project(':sublibs:OSMBonusPack')

	compile files('libs/public-transport-enabler.jar')
	// needed by PTE to avoid ClassNotFound error
	compile 'com.google.guava:guava:18.0'
}

android {
	defaultConfig {
		versionCode 16
		versionName "0.6.1"

		compileSdkVersion 22
		targetSdkVersion 22
		buildToolsVersion "22.0.1"
		minSdkVersion 12
	}

	buildTypes {
		release {
			resValue "string", "app_name", "Liberario"
		}

		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			resValue "string", "app_name", "Liberario Devel"
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (project.hasProperty('signingStoreLocation') &&
			project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') &&
			project.hasProperty('signingKeyPassword')) {
		println "Found sign properties in gradle.properties! Signing buildâ€¦"

		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}

		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	// Do not abort build if lint finds errors
	lintOptions {
		abortOnError false
	}
}

